plugins {
    id 'com.google.protobuf' version '0.9.4'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.4'
    
    // Database (MySQL)
    runtimeOnly 'com.mysql:mysql-connector-j:8.3.0'
    
    // gRPC client dependencies
    implementation 'net.devh:grpc-client-spring-boot-starter:2.15.0.RELEASE'
    implementation 'io.grpc:grpc-core:1.58.0'
    implementation 'io.grpc:grpc-protobuf:1.58.0'
    implementation 'io.grpc:grpc-stub:1.58.0'
    
    // Protobuf
    implementation 'com.google.protobuf:protobuf-java:3.25.1'
    
    compileOnly 'org.projectlombok:lombok'
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
    annotationProcessor 'org.projectlombok:lombok'
    
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.4'
    
    // Testcontainers
    testImplementation 'org.testcontainers:testcontainers:1.19.8'
    testImplementation 'org.testcontainers:mysql:1.19.8'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.8'
    
    // DBRider
    testImplementation 'com.github.database-rider:rider-junit5:1.43.0'
    testImplementation 'com.github.database-rider:rider-spring:1.43.0'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.1"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.58.0"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

sourceSets {
    main {
        proto {
            srcDirs '../proto'
        }
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

test {
    useJUnitPlatform {
        // コマンドラインからタグを指定可能
        // 例: ./gradlew test -PincludeTags=Read
        // 例: ./gradlew test -PexcludeTags=Slow
        if (project.hasProperty('includeTags')) {
            includeTags project.property('includeTags').split(',')
        }
        if (project.hasProperty('excludeTags')) {
            excludeTags project.property('excludeTags').split(',')
        }
    }
}

